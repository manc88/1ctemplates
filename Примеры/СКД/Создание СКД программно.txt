Для начала веберем объект метаданных. Дерево метаданных строится из наличия таковых в любой тестовой конфигурации для версии 8.2 и выше (Управляемые формы). Далее строим макет, используя доступные поля объекта. Группировки, таблицы группировок со строками и колонками, ресурсы - настраиваем (как нам удобно) в визуально-интерактивном режиме . Жмем “Сформировать” на вкладке Результат, и вуаля! Изучаем, что да как.

Приложенная в качестве примера обработка формирует отчет с помощью СКД на любой тестовой конфигурации. На форме обработки реализована возможность Drag'n'Drop добавления строк и колонок в таблицах группировки СКД.  

 С помощью обработки реализуются учебные цели:

1. Создние универсального отчета на СКД
2. Вывод двух таблиц на форму: 1-я должна содержать выбранные поля отчета, 2-я - все остальные доступные поля СКД
3. Вывод на форму трех таблиц: 1-я строки для компановки, 2-я столбцы, 3-я ресурсы - первоначально это пустые таблицы
4. Должен работать Drag'n'Drop для перетаскивания полей указанных в п.2 в поля указанные в п.3
5. Реализация заполнение отчета исходя из п. 3 настроек

СКД = Новый СхемаКомпоновкиДанных; 
    
    // При необходимости можно получить ранее созданный макет СКД
    //ОтчетОбъект  = РеквизитФормыВЗначение("Отчет");
    //СКД = ОтчетОбъект.ПолучитьМакет("ОсновнаяСхемаКомпоновкиДанных"); 
    
    // определим источник данных для схемы 
    Источник = СКД.ИсточникиДанных.Добавить();
    Источник.Имя = "ЛокальнаяБаза";
    Источник.СтрокаСоединения = "";
    Источник.ТипИсточникаДанных = "Local"; 
    
    НаборДанных = СКД.НаборыДанных.Добавить(Тип("НаборДанныхЗапросСхемыКомпоновкиДанных"));
    НаборДанных.Имя = "Продажи";
    НаборДанных.ИсточникДанных = "ЛокальнаяБаза";
    НаборДанных.АвтоЗаполнениеДоступныхПолей = Истина;
   
Следует предусмотреть вариант с (жестким) установленным запросом набора данных, когда необходимо программно создавать нужные нам поля группировки, для формирования жесткой структуры отчета:

НаборДанных.Запрос = (
    
    "ВЫБРАТЬ
    |    ТоварыНаСкладахОстаткиИОбороты.Регистратор,
    |    ТоварыНаСкладахОстаткиИОбороты.Склад,
    |    ТоварыНаСкладахОстаткиИОбороты.Номенклатура,
    |    ТоварыНаСкладахОстаткиИОбороты.Партия,
    |    ТоварыНаСкладахОстаткиИОбороты.КоличествоРасход КАК Количество,
    |    ТоварыНаСкладахОстаткиИОбороты.СуммаРасход КАК Сумма,
    |    ТоварыНаСкладахОстаткиИОбороты.СуммаРасход / ТоварыНаСкладахОстаткиИОбороты.КоличествоРасход КАК Стоимость
    |ИЗ
    |    РегистрНакопления.ТоварыНаСкладах.ОстаткиИОбороты(&НачалоПериода, &КонецПериода, Регистратор, , ) КАК ТоварыНаСкладахОстаткиИОбороты
    |ГДЕ
    |    ТоварыНаСкладахОстаткиИОбороты.Регистратор ССЫЛКА Документ.РасходнаяТоварнаяНакладная"    
    
    );
    
    // добавим поля
    НаборДанных.АвтоЗаполнениеДоступныхПолей = Истина;
    
    //Ручное заполнение полей при необходимости (пример, используем для описания полей внешнего источника)
    //ПолеНабораДанных             = НаборДанных.Поля.Добавить(Тип("ПолеНабораДанныхСхемыКомпоновкиДанных"));
    //ПолеНабораДанных.Заголовок   = "Номенклатура";
    //ПолеНабораДанных.ПутьКДанным = "Номенклатура";
    //ПолеНабораДанных.Поле        = "Номенклатура";
    //ПолеНабораДанных.ТипЗначения = Тип("СправочникСсылка.Номенклатура");
    
    //ПолеНабораДанных             = НаборДанных.Поля.Добавить(Тип("ПолеНабораДанныхСхемыКомпоновкиДанных"));
    //ПолеНабораДанных.Заголовок   = "Количество";
    //ПолеНабораДанных.ПутьКДанным = "Количество";
    //ПолеНабораДанных.Поле        = "Количество";
    //ПолеНабораДанных.ТипЗначения = Тип("Число");
    //
    //ПолеНабораДанных             = НаборДанных.Поля.Добавить(Тип("ПолеНабораДанныхСхемыКомпоновкиДанных"));
    //ПолеНабораДанных.Заголовок   = "Стоимость";
    //ПолеНабораДанных.ПутьКДанным = "Стоимость";
    //ПолеНабораДанных.Поле        = "Стоимость";
    //ПолеНабораДанных.ТипЗначения = Тип("Число");
    
    // определим пареметры  
    ПараметрДанных               = СКД.Параметры.Добавить();
    ПараметрДанных.Имя           = "НачалоПериода";
    ПараметрДанных.Использование = ИспользованиеПараметраКомпоновкиДанных.Всегда;
    ПараметрДанных.Заголовок     = "Начало периода";
    ПараметрДанных.ВключатьВДоступныеПоля = Истина;
    
    ПараметрДанных               = СКД.Параметры.Добавить();
    ПараметрДанных.Имя           = "КонецПериода";
    ПараметрДанных.Использование = ИспользованиеПараметраКомпоновкиДанных.Всегда;
    ПараметрДанных.Заголовок     = "Конец периода";
    ПараметрДанных.ВключатьВДоступныеПоля = Истина;
    
    // определим ресурсы, для примера поле "Сумма"
    ПолеРесурса             = СКД.ПоляИтога.Добавить();
    ПолеРесурса.Выражение   = "Сумма(Сумма)";
    ПолеРесурса.ПутьКДанным = "Сумма";
    
    // получим настройки для схемы 
    НастройкиСКД = СКД.НастройкиПоУмолчанию;
    
    //Добавим выбранные поля к нашему отчету (Эти поля будут наследоватся автополем в группировках ниже)
    ВыбранноеПолеНоменклатура           = НастройкиСКД.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
    ВыбранноеПолеНоменклатура.Поле      = Новый ПолеКомпоновкиДанных("Номенклатура");
    ВыбранноеПолеНоменклатура.Заголовок = "Номенклатура";
    
    ВыбранноеПолеКоличество           = НастройкиСКД.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
    ВыбранноеПолеКоличество.Поле      = Новый ПолеКомпоновкиДанных("Количество");
    ВыбранноеПолеКоличество.Заголовок = "Количество";
        
    // добавим поле Сумма в выбранные поля - необходимо для программного добавления этого поля в ресурс
    ВыбранноеПолеСумма           = НастройкиСКД.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
    ВыбранноеПолеСумма.Поле      = Новый ПолеКомпоновкиДанных("Сумма");
    ВыбранноеПолеСумма.Заголовок = "Сумма";
        
    //// определим структуру --->>>
    
    //добавим группировку СКЛАД --
    ГруппировкаПоСкладу     = НастройкиСКД.Структура.Добавить(Тип("ГруппировкаКомпоновкиДанных"));
    ГруппировкаПоСкладу.Имя = "Склад";
    
    //созданная группировка по полю "Склад"
    ПолеГруппировкиСклад                = ГруппировкаПоСкладу.ПоляГруппировки.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));       
    ПолеГруппировкиСклад.Поле           = Новый ПолеКомпоновкиДанных("Склад");
    ПолеГруппировкиСклад.ТипГруппировки = ТипГруппировкиКомпоновкиДанных.Элементы;
    ПолеГруппировкиСклад.ТипДополнения  = ТипДополненияПериодаКомпоновкиДанных.БезДополнения;
    
    //добавить Автополя в группировке Склад
    ВыбранноеАвтоПоле = ГруппировкаПоСкладу.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));     
    
    //Добавим группировку детальные записи к группировке склад
    ДетальныеЗаписиГруппировкиСклад = ГруппировкаПоСкладу.Структура.Добавить(Тип("ГруппировкаКомпоновкиДанных"));
    АвтоПолеГруппировки = ДетальныеЗаписиГруппировкиСклад.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
    АвтоПолеГруппировки.Использование = Истина;
    
    // таблица группировки (создание таблицы группировки с автополем выбора)--
    ТаблицаГруппировкаОтчета = НастройкиСКД.Структура.Добавить(Тип("ТаблицаКомпоновкиДанных"));
    АвтоПолеГруппировки = ТаблицаГруппировкаОтчета.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
    АвтоПолеГруппировки.Использование = Истина;
    
    // Создадим строки таблицы группировки --
    
    //// Пример создания строки таблицы группировки СКД (строка группировки детальные записи с автополем)
    //ГруппировкаСтроки = ТаблицаГруппировкаОтчета.Строки.Добавить();    
    //АвтоПолеГруппировкаДетальныеЗаписи = ГруппировкаСтроки.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных")); 
    //АвтоПолеГруппировкаДетальныеЗаписи.Использование = Истина;
    
    //// пример заполнения выбранных полей строки группировки детальные записи
    //ПолеГруппировкаДетальныеЗаписи = ГруппировкаСтроки.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
    //ПолеГруппировкаДетальныеЗаписи.Поле = Новый ПолеКомпоновкиДанных("Склад");
    //ПолеГруппировкаДетальныеЗаписи.Использование = Истина; <-- конец примера
    
    // создадим строку таблицы группировки "Номенклатура"
    ГруппировкаСтроки = ТаблицаГруппировкаОтчета.Строки.Добавить();     
    ГруппировкаСтроки.Использование = Истина;
    
    // определим поля группировки в строке таблицы группировки 
    ПолеГруппировки                = ГруппировкаСтроки.ПоляГруппировки.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));      
    ПолеГруппировки.Поле           = Новый ПолеКомпоновкиДанных("Номенклатура");
    ПолеГруппировки.ТипГруппировки = ТипГруппировкиКомпоновкиДанных.Элементы;
    ПолеГруппировки.ТипДополнения  = ТипДополненияПериодаКомпоновкиДанных.БезДополнения;
    
    // добавим автополе в выбранные поля строки "Номенклатура"
    АвтоПолеГруппировкаДетальныеЗаписи = ГруппировкаСтроки.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных")); 
    АвтоПолеГруппировкаДетальныеЗаписи.Использование = Истина;
    
    // колонки таблицы группировки (Создание детальных записей)
    ГруппировкаКолонки = ТаблицаГруппировкаОтчета.Колонки.Добавить();
    ГруппировкаКолонки.Имя = "<Детальные записи>";
    АвтоПолеГруппировкаДетальныеЗаписи = ГруппировкаКолонки.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
    АвтоПолеГруппировкаДетальныеЗаписи.Использование = Истина;





//Допинфо: http://its.1c.ru/db/metod8dev#content:3450:hdoc




